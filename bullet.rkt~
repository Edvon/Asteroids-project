#lang racket
(require racket/gui)
(provide bullet%)
(require "utilities.rkt")

;; Purpose: Defines a bullet in our game and its properties.
;;
;; Last modified: 16-05-19
;;
;; AUTHORS: Oscar GÃ¶ransson and Edvin Ljungstrand.

(define bullet%
  (class object%

    ;;[field]         [comment]

    ;;[creator]       [The object wich created the bullet]
    ;;[id])           [Id of the bullet.]
    ;;[name]          [Name of the bullet.]
    ;;[xpos]          [X-position for the bullet.]
    ;;[ypos]          [y-position for the bullet.]
    ;;[dx]            [Bullet's speed in x-direction.]
    ;;[dy]            [Bullet's speed in y-direction.]

    ;;[health]        [Health of the bullet.]
    ;;[diameter]      [Diameter of the bullet.]
    ;;[radius]        [Radius of the bullet.]
    ;;[points]        [Number of points the object is worth.]
    ;;[mid-xpos]      [X-pos for middle of bullet.]
    ;;[mid-ypos]      [Y-pos for middle of bullet.]   
    ;;[image]         [Image of the bullet.]
    
    (init-field
     [creator null] 
     [id 3]         
     [name null]   
     [xpos 0]       
     [ypos 0]       
     [dx 0]         
     [dy 0])        
    
    (field [health 2]     
           [diameter 8]   
           [radius (/ diameter 2)]      
           [points 0]   
           [mid-xpos (+ xpos radius)]  
           [mid-ypos (+ ypos radius)]   
           [image (make-bitmap diameter diameter)]) 
    
    (super-new)

    (create-bullet-image image)
    
    ;; METHOD: get-image
    ;;
    ;; DESCRIPTION: Provides the image of the bullet.
    ;;
    ;; INPUT: none
    ;;
    ;; OUTPUT: image
    (define/public (get-image)
      image)
    
    ;; METHOD: get-mid-x and get-mid-y
    ;;
    ;; DESCRIPTION: Provides the middle position of the bullet.
    ;;
    ;; INPUT: none
    ;;
    ;; OUTPUT: #<void>
    (define/public (get-mid-xpos)
      mid-xpos) 
    (define/public (get-mid-ypos)
      mid-ypos)
    
    ;; METHOD: set-mid-x! and set-mid-y!
    ;;
    ;; DESCRIPTION: Lets us set the position depending on the middle position.
    ;;
    ;; INPUT: New position for middle of the bullet.
    ;;
    ;; OUTPUT: #<void>
    (define/public (set-mid-x! new-mid-x)
      (set! xpos (- new-mid-x radius)))  
    (define/public (set-mid-y! new-mid-y)
      (set! ypos (- new-mid-y radius)))

    ;; METHOD: obj-has-collided-with
    ;;
    ;; DESCRIPTION: When bullet has collided the creator of the bullets
    ;; score is updated with the points for the object wich has collided
    ;; with the bullet and the bullet is then deleted.
    ;;
    ;; INPUT: Colliding object
    ;;
    ;; OUTPUT: #<void>
    (define/public (obj-has-collided-with obj)
      (unless (null? creator)
        (send creator update-score (get-field points obj)))
      (destroy! name))

    ;; METHOD: create-bullet-image
    ;;
    ;; DESCRIPTION: A method wich draws a bullet on a bitmap object.
    ;;
    ;; INPUT: bitmap-target
    ;;
    ;; OUTPUT: #<void>
    (define/private (create-bullet-image bitmap-target)
      (let ((dc (new bitmap-dc% [bitmap bitmap-target])))
        (send dc set-pen "white" 1 'solid)
        (send dc set-brush "black" 'solid)
        (send dc draw-ellipse 0 0 diameter diameter)))

    ;; METHOD: destroy!
    ;;
    ;; DESCRIPTION: Destroys the bullet by removeing it from its hash-list.
    ;;
    ;; INPUT: Name of the bullet wich will be destroyed.
    ;;
    ;; OUTPUT: #<void>
    (define (destroy! name)
      (hash-remove! bullets-hash name))

    ;; METHOD: update
    ;;
    ;; DESCRIPTION: Parameters provided by bullet objects, which are stored in
    ;; the bullet-hash, calculates the bullets' new positions and draws them.
    ;;
    ;; INPUT: dc - drawing context
    ;;
    ;; Output: #<void>  
    (define/public (update! dc)
      (send dc draw-bitmap image xpos ypos)
      (set! health (- health 0.05))    
      (when (< health 0.001)
        (destroy! name))
      (set! xpos (+ xpos dx))
      (set! ypos (+ ypos dy))
      (set! mid-xpos (+ xpos radius))
      (set! mid-ypos (+ ypos radius)))))